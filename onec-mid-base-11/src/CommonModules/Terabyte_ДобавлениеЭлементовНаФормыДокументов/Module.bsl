	// Terabyte Столповский Д.А. 03.01.2024 Версия конфигурации в момент внесения изменений: 1.0.1.1
	// Данный модуль предназначен для программного изменения форм Документов. Вызывается из типового модуля ПодключаемыеКоманды.ПриСозданииНаСервере.
	// Указанный модуль вызывается при условии вызова пользователем определенных форм и срабатываем при создании форм на сервере.
Процедура ДобавитьЭлементыНаФормыДокументов (Форма, ИмяФормы) Экспорт	
	// Terabyte Столповский Д.А. 07.01.2024 Версия конфигурации в момент внесения изменений: 1.0.1.1

	// Подменяем ПараметрыВыбора в поле "Договор" в документе "ОплатаОтПокупателя",
	// так как в типовой конфигурации допущена ошибка при создании реквизита "Договор": 
	//В поле "Договор" в документе "ОплатаОтПокупателя" назначен отбор на отображение договоров не с покупателями, а с поставщиками, таким образом мы выбирая в документе Покупателя, не можем выбрать нужный договор.
	Если ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента"	Тогда 
		ИзменениеПараметровОтбораДокументаОплатаПокупателя(Форма);
	КонецЕсли;
	
	// Вставляем в Заказ клиента перед табличными частями новую группу с 2 элементами: Полем "Скидка" и кнопкой "Установить скидку"	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ВставкаДвухПолейСкидкиНаДокументЗаказКлиента(Форма);
	КонецЕсли;
	
	// Вставляем во все документы новое поле с выбором контактных лиц под полем "Договор" в той же группе, где находится "Договор"	
	ДобавлениеПоляВводаКонтактныеЛица(Форма, ИмяФормы); 
	// Конец изменений Столповский Д.А. 07.01.2024
КонецПроцедуры

Процедура ИзменениеПараметровОтбораДокументаОплатаПокупателя(Форма)
	// Terabyte Столповский Д.А. 07.01.2024 Версия конфигурации в момент внесения изменений: 1.0.1.1
	// Отдельная процедура, которая выполняет отдельную задачу изменения Параметров отбора в Поле Договор в Документе Оплата Покупателем
	ЭлементФормОплатыПокупалетяДоговор = Форма.Элементы.Договор;
	МассивПараметровВыбораДоговора = Новый Массив;
	НовыйЭлементВыбораДоговора = Новый ПараметрВыбора("Отбор.ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.Покупатель);
	МассивПараметровВыбораДоговора.Добавить(НовыйЭлементВыбораДоговора);
	ФисированныйМассивПараметровВыбораДоговора = Новый ФиксированныйМассив(МассивПараметровВыбораДоговора);
	ЭлементФормОплатыПокупалетяДоговор.ПараметрыВыбора = ФисированныйМассивПараметровВыбораДоговора;
	// Конец изменений Столповский Д.А. 07.01.2024
КонецПроцедуры

Процедура ВставкаДвухПолейСкидкиНаДокументЗаказКлиента(Форма)
	// Terabyte Столповский Д.А. 07.01.2024 Версия конфигурации в момент внесения изменений: 1.0.1.1
	// Отдельная процедура, которая выполняет отдельную задачу создания поля Скидки и кнопки Установить скидку в документе Заказ Покупателя
		ГруппаСкидки = Форма.Элементы.Вставить("ГруппаСкидки", Тип("ГруппаФормы"), Форма, Форма.Элементы.Страницы);
		ГруппаСкидки.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		ГруппаСкидки.Заголовок = "";
		ПолеВвода = Форма.Элементы.Добавить("Terabyte_РазмерСкидки", Тип("ПолеФормы"), Форма.Элементы.ГруппаСкидки);
		ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
		ПолеВвода.Заголовок = "Разрер скидки";
		ПолеВвода.ПутьКДанным = "Объект.Terabyte_РазмерСкидки";
		ПолеВвода.УстановитьДействие("ПриИзменении", "Terabyte_УстановитьСкидку");
		КнопкаФормы = Форма.Элементы.Добавить("Terabyte_УстановитьСкидку", Тип("КнопкаФормы"), Форма.Элементы.ГруппаСкидки);
		КнопкаФормы.ИмяКоманды = "Terabyte_УстановитьСкидку";
		КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;	
	// Конец изменений Столповский Д.А. 09.01.2024
КонецПроцедуры

Процедура ДобавлениеПоляВводаКонтактныеЛица(Форма, ИмяФормы) 
	// Terabyte Столповский Д.А. 07.01.2024 Версия конфигурации в момент внесения изменений: 1.0.1.1
	// Отдельная процедура, которая выполняет отдельную задачу создания поля Контактное лицо во всех документах.
	ЭлементГруппаШапкаПраво = Форма.Элементы.Найти("ГруппаШапкаПраво");
	Если ЭлементГруппаШапкаПраво = Неопределено Тогда
		Если ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
			ПолеВвода = Форма.Элементы.Вставить("Terabyte_КонтактныеЛица", Тип("ПолеФормы"), , Форма.Элементы.Основание);
		ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
			ПолеВвода = Форма.Элементы.Вставить("Terabyte_КонтактныеЛица", Тип("ПолеФормы"), , Форма.Элементы.СуммаДокумента);
		КонецЕсли;
	Иначе
		ПолеВвода = Форма.Элементы.Добавить("Terabyte_КонтактныеЛица", Тип("ПолеФормы"), ЭлементГруппаШапкаПраво);
	КонецЕсли;	
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.Заголовок = "Контактное лицо";
	ПолеВвода.ПутьКДанным = "Объект.Terabyte_КонтактныеЛица"; 
	// Конец изменений Столповский Д.А. 07.01.2024
КонецПроцедуры
